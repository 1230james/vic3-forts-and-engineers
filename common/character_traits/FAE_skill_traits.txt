FAE_trait_combat_engineer_1 = {
    type = skill
    texture = "gfx/interface/icons/character_trait_icons/FAE_trait_combat_engineer_1.dds"

    command_modifier = {
        character_fae_battle_condition_attacker_fort_breached_mult = 1
        character_battle_condition_dug_in_mult = 0.5
        character_battle_condition_mud_mult    = -0.25
    }

    possible = {
        has_role = general
        owner ?= {
            has_technology_researched = field_works
		}
        NOR = {
            has_trait = FAE_trait_combat_engineer_2
            has_trait = FAE_trait_combat_engineer_3
        }
        NOT = {
            age < define:NCharacters|ADULT_AGE
        }
    }

    weight = {
        value = 5
        
        # Bonus from already being Engineer
        # Anyone can roll this but apparently only TU Agitators get a bonus for it? lol
        if = {
            limit = {
                has_trait = engineer
            }
            multiply = 2
        }
        
        # Bonus for researching Trench Works
        if = {
            limit = {
                owner ?= {
                    has_technology_researched = trench_works
                }
            }
            multiply = 2
        }
        
        # Bonus for defender specialist (e.g. experience with trench warfare)
        if = {
            limit = {
                has_trait = stalwart_defender
            }
            multiply = 2
        }
        else_if = {
            limit = {
                has_trait = trench_rat
            }
            multiply = 4
        }
        else_if = {
            limit = {
                has_trait = defense_in_depth_specialist
            }
            multiply = 8
        }
        
        # Bonus for having Combat Engineer mob. option at war
        if = {
            limit = {
                commander_military_formation ?= {
                    is_mobilized = yes
                    has_mobilization_option = mobilization_option:FAE_mo_combat_engineers
                }
            }
            multiply = 5
        }
    }

    value = 1
}

FAE_trait_combat_engineer_2 = {
    type = skill
    texture = "gfx/interface/icons/character_trait_icons/FAE_trait_combat_engineer_2.dds"

    command_modifier = {
        character_fae_battle_condition_attacker_fort_breached_mult = 2
        character_battle_condition_dug_in_mult = 1
        character_battle_condition_mud_mult    = -0.5
    }

    possible = {
        has_role  = general
        has_trait = FAE_trait_combat_engineer_1
        NOT = {
            age < define:NCharacters|ADULT_AGE
        }
    }

    weight = {
        value = 5
        
        # Bonus from already being Engineer
        if = {
            limit = {
                has_trait = engineer
            }
            multiply = 2
        }
        
        # Bonus for researching Trench Works
        if = {
            limit = {
                owner ?= {
                    has_technology_researched = trench_works
                }
            }
            multiply = 2
        }
        
        # Bonus for defender specialist (e.g. experience with trench warfare)
        if = {
            limit = {
                has_trait = stalwart_defender
            }
            multiply = 2
        }
        else_if = {
            limit = {
                has_trait = trench_rat
            }
            multiply = 4
        }
        else_if = {
            limit = {
                has_trait = defense_in_depth_specialist
            }
            multiply = 8
        }
        
        # Bonus for having Combat Engineer mob. option at war
        if = {
            limit = {
                commander_military_formation ?= {
                    is_mobilized = yes
                    has_mobilization_option = mobilization_option:FAE_mo_combat_engineers
                }
            }
            multiply = 5
        }
    }
    
    replace = {
        FAE_trait_combat_engineer_1
    }

    value = 2
}

FAE_trait_combat_engineer_3 = {
    type = skill
    texture = "gfx/interface/icons/character_trait_icons/FAE_trait_combat_engineer_3.dds"

    command_modifier = {
        character_fae_battle_condition_attacker_fort_breached_mult = 3
        character_battle_condition_dug_in_mult = 1.5
        character_battle_condition_mud_mult    = -0.75
    }

    possible = {
        has_role  = general
        has_trait = FAE_trait_combat_engineer_2
        NOT = {
            age < define:NCharacters|ADULT_AGE
        }
    }

    weight = {
        value = 5
        
        # Bonus from already being Engineer
        if = {
            limit = {
                has_trait = engineer
            }
            multiply = 2
        }
        
        # Bonus for researching Trench Works
        if = {
            limit = {
                owner ?= {
                    has_technology_researched = trench_works
                }
            }
            multiply = 2
        }
        
        # Bonus for researching Stormtroopers (Infiltrators)
        # Tier 3 exclusive
        if = {
            limit = {
                owner ?= {
                    has_technology_researched = stormtroopers
                }
            }
            multiply = 2
        }
        
        # Bonus for defender specialist (e.g. experience with trench warfare)
        if = {
            limit = {
                has_trait = stalwart_defender
            }
            multiply = 2
        }
        else_if = {
            limit = {
                has_trait = trench_rat
            }
            multiply = 4
        }
        else_if = {
            limit = {
                has_trait = defense_in_depth_specialist
            }
            multiply = 8
        }
        
        # Bonus for having Combat Engineer mob. option at war
        if = {
            limit = {
                commander_military_formation ?= {
                    is_mobilized = yes
                    has_mobilization_option = mobilization_option:FAE_mo_combat_engineers
                }
            }
            multiply = 5
        }
    }
    
    replace = {
        FAE_trait_combat_engineer_1
        FAE_trait_combat_engineer_2
    }

    value = 3
}
